buildscript {

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${version_springBoot}"
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.11.0'
        classpath "se.transmode.gradle:gradle-docker:${version_gradleDocker}"
    }

}

apply plugin: 'spring-boot'
apply plugin: 'mongo'
apply plugin: 'docker'

dependencies {
    compile(
            'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.boot:spring-boot-starter-data-mongodb'
    )

    testCompile(
            project(':test-base'),
            'org.springframework.boot:spring-boot-starter-test'
    )
}

test {
    useJUnit {
        excludeCategories 'com.github.reflectoring.infiniboard.test.categories.MongoIntegrationTests'
    }
}

bootRepackage {
    mainClass = 'com.github.reflectoring.infiniboard.harvester.HarvesterApplication'
    executable = true
}

task integrationTest(type: Test) {
    runWithMongoDb = true
    useJUnit {
        includeCategories 'com.github.reflectoring.infiniboard.test.categories.MongoIntegrationTests'
    }
}

task buildDocker(type: Docker, dependsOn: build) {
//  push = true
    tagVersion = project.version
    tag = "${project.dockerGroup}/${jar.baseName}"

    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from("${project.buildDir}/libs/") {
                rename "${jar.baseName}-.*\\.jar", "${jar.baseName}.jar"
            }
            into stageDir
            exclude '*.original'
        }
    }
}
